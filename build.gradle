plugins {
    id 'com.android.library' version '8.1.0'
    id 'org.jetbrains.kotlin.android' version '1.9.0'
    id 'maven-publish'
}

android {
    namespace = "com.clevertree.jscbridge"
    compileSdk = 33

    defaultConfig {
        minSdk = 24
        targetSdk = 33
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.webkit:android-jsc:+'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.clevertree'
                artifactId = 'jscbridge'
                version = '1.0.0'

                pom {
                    name = 'JSC Bridge'
                    description = 'Cross-platform JavaScriptCore bridge for Relay'
                    url = 'https://github.com/clevertree/jscbridge'
                }
            }
        }
        repositories {
            mavenLocal()
        }
    }
}
